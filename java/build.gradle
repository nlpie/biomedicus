plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}


def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    def execResult = exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
        ignoreExitValue = true
    }
    if (execResult.getExitValue() != 0) {
        return "0.0.0+development-SNAPSHOT"
    }
    def pattern = ~/v([0-9]+)(?:.([0-9]+))?(?:.([0-9]+))?(?:-(alpha|beta|pre|rc).([0-9]+))?(?:-([0-9]+)-([a-g0-9]+))?(-dirty)?/
    def newVersion = stdout.toString().trim().replaceFirst(pattern) { _,major,minor,patch,pre,preVersion,com,hash,dirty ->
        def incremented = false
        def result = ''
        if (com != null || hash != null || dirty != null) {
            result = '-SNAPSHOT'
        } else {
            incremented = true
        }
        if (pre != null && preVersion != null) {
            if (!incremented) {
                preVersion = (preVersion as int) + 1
                incremented = true
            }
            result = "-${pre}${preVersion}${result}"
        }
        if (patch != null) {
            if (!incremented) {
                patch = (patch as int) + 1
                incremented = true
            }
            result = ".${patch}${result}"
        }
        if (minor != null) {
            if (!incremented) {
                minor = (minor as int) + 1
                incremented = true
            }
            result = ".${minor}${result}"
        }
        if (!incremented) {
            major = (major as int) + 1
        }
        result = "${major}${result}"
        return result
    }
    return newVersion
}

group 'edu.umn.biomedicus'
version gitVersion()

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '26.0.2'

    implementation group: 'edu.umn.nlpie', name: 'mtap', version: '1.4.+'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'
    implementation group: 'args4j', name: 'args4j', version: '2.37'
    implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '9.10.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.13.0'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.3'
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.2'

    implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: '1.70.0'
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: '1.70.0'
    implementation group: 'io.grpc', name: 'grpc-stub', version: '1.70.0'
    implementation group: 'io.grpc', name: 'grpc-services', version: '1.70.0'

    runtimeOnly group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.5'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: '2.24.1'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.1'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.11.4'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.11.4'
}

test {
    useJUnitPlatform()
}

shadowJar {
    manifest {
        attributes 'Implementation-Title': 'BioMedICUS',
                'Description': 'A system for large-scale text analysis and processing of biomedical and clinical reports.',
                'Implementation-Version': archiveVersion
    }
    mergeServiceFiles()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

task execute(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = project.hasProperty('mainClass') ? project.getProperties().get('mainClass') : null
}

task conceptsUtility(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main 'edu.umn.biomedicus.concepts.ConceptsUtility'
}

task writeVersion() {
    doLast {
        new File(buildDir, "version.txt").text = "$version\n"
    }
}
